buildscript {
    ext.kotlin_version = '1.1.0'

    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'kotlin-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.brunoaybar.unofficialupc"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 4
        versionName "1.0.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        prod {
            buildConfigField 'String', 'REST_URL', '"***REMOVED***"'
            buildConfigField 'String', 'AMPLITUDE_SDK', '"***REMOVED***"'
        }
    }

    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    // We want to make sure, we do not allow accidentally (for now ;))
    // any Kotlin code in production by also adding this defensive lines:
    afterEvaluate {
        android.sourceSets.all { sourceSet ->
            if (!sourceSet.name.startsWith('test') || !sourceSet.name.startsWith('androidTest')) {
                sourceSet.kotlin.setSrcDirs([])
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    testCompile 'junit:junit:4.12'

    //General dependencies
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile "com.google.guava:guava:20.0"
    compile 'com.github.lzyzsd:circleprogress:1.1.0@aar'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.roughike:bottom-bar:2.0.2'
    compile 'cn.pedant.sweetalert:library:1.3'

    // RxJava
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'

    //Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'

    // Mockito
    testCompile 'org.mockito:mockito-core:1.10.19'

    //Analytics
    compile 'com.amplitude:android-sdk:2.7.1'
    compile 'com.google.firebase:firebase-core:9.4.0'
    compile 'com.google.firebase:firebase-config:9.4.0'
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    //Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.0"

    //Tests w/ Kotlin
    testCompile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.0'
    testCompile 'org.jetbrains.kotlin:kotlin-test-junit:1.1.0'
    testCompile "com.nhaarman:mockito-kotlin:1.1.0"
    testCompile 'org.amshove.kluent:kluent:1.14'
    testCompile "org.robolectric:robolectric:3.2.1"
}
